grammar org.eclipse.osee.framework.OseeTypes with org.eclipse.xtext.common.Terminals

generate oseeTypes "http://www.eclipse.org/osee/framework/OseeTypes"

OseeTypeModel :
	(imports+=Import)*
	(artifactTypes += XArtifactType | relationTypes += XRelationType | attributeTypes += XAttributeType | enumTypes += XOseeEnumType | enumOverrides += XOseeEnumOverride)*;

Import :
	'import' importURI=STRING;
	
terminal WHOLE_NUM_STR: ('0'..'9')+;

NAME_REFERENCE: STRING;
QUALIFIED_NAME : ID ('.' ID)*;

OseeElement:
	XOseeEnumOverride | OseeType;

OseeType:
	XArtifactType | XRelationType | XAttributeType | XOseeEnumType;

XArtifactType:
	(abstract?='abstract')? 'artifactType' name=NAME_REFERENCE ('extends' superArtifactTypes+=[XArtifactType|NAME_REFERENCE] (',' superArtifactTypes+=[XArtifactType|NAME_REFERENCE])*)? '{'
		'guid' typeGuid = STRING
	    (validAttributeTypes+=XAttributeTypeRef)*
	'}';

XAttributeTypeRef:
	'attribute' validAttributeType=[XAttributeType|NAME_REFERENCE] ('branchGuid' branchGuid=STRING)?;

XAttributeType:
		'attributeType' name=NAME_REFERENCE ('extends' baseAttributeType=AttributeBaseType) ('overrides' override=[XAttributeType|NAME_REFERENCE])? '{'
		'guid' typeGuid = STRING
		'dataProvider' dataProvider = ('DefaultAttributeDataProvider' | 'UriAttributeDataProvider' | QUALIFIED_NAME)
		'min' min=WHOLE_NUM_STR
		'max' max=(WHOLE_NUM_STR | 'unlimited')
		('taggerId' taggerId = ('DefaultAttributeTaggerProvider' | QUALIFIED_NAME))?
		('enumType' enumType = [XOseeEnumType|NAME_REFERENCE])?
		('description' description = STRING)?
		('defaultValue' defaultValue = STRING)?
		('fileExtension' fileExtension = STRING)?
	'}';

AttributeBaseType:
	'BooleanAttribute' | 'CompressedContentAttribute' | 'DateAttribute' | 'EnumeratedAttribute' | 'FloatingPointAttribute' | 'IntegerAttribute' | 'JavaObjectAttribute' | 'StringAttribute' | 'WordAttribute' | QUALIFIED_NAME;

XOseeEnumType:
	'oseeEnumType' name=NAME_REFERENCE '{'
		'guid' typeGuid = STRING
		(enumEntries+=XOseeEnumEntry)*
	'}';

XOseeEnumEntry:
	'entry' name=NAME_REFERENCE (ordinal=WHOLE_NUM_STR)? ('entryGuid' entryGuid = STRING)?;

XOseeEnumOverride:
	'overrides enum' overridenEnumType = [XOseeEnumType|NAME_REFERENCE] '{'
		(inheritAll ?= 'inheritAll')?
		(overrideOptions += OverrideOption)*
	'}';

OverrideOption:	
	AddEnum | RemoveEnum;
	
AddEnum: 
	'add' enumEntry = NAME_REFERENCE (ordinal=WHOLE_NUM_STR)? ('entryGuid' entryGuid = STRING)?;

RemoveEnum:
	'remove' enumEntry = [XOseeEnumEntry|NAME_REFERENCE];

XRelationType:
	'relationType' name=NAME_REFERENCE '{'
		'guid' typeGuid = STRING
		'sideAName' sideAName=STRING
		'sideAArtifactType' sideAArtifactType=[XArtifactType|NAME_REFERENCE]
		'sideBName' sideBName=STRING
		'sideBArtifactType' sideBArtifactType=[XArtifactType|NAME_REFERENCE]
		'defaultOrderType' defaultOrderType=RelationOrderType
		'multiplicity' multiplicity=RelationMultiplicityEnum
	'}';
	
enum RelationMultiplicityEnum :  
	ONE_TO_ONE | ONE_TO_MANY | MANY_TO_ONE | MANY_TO_MANY;

RelationOrderType:
	'Lexicographical_Ascending' | 'Lexicographical_Descending' | 'Unordered' | ID;